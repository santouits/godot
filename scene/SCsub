#!/usr/bin/env python

Import("env")

env.scene_sources = []

# Thirdparty code
thirdparty_dir = "#thirdparty/misc/"
thirdparty_sources = [
    # C++ sources
    "easing_equations.cpp",
    # C sources
    "mikktspace.c",
]
thirdparty_sources = [thirdparty_dir + file for file in thirdparty_sources]

env_thirdparty = env.Clone()
env_thirdparty.disable_warnings()
env_thirdparty.add_source_files(env.scene_sources, thirdparty_sources)

# Godot's own sources
env.add_source_files(env.scene_sources, "*.cpp")

scene_env = env.Clone()
scene_env.Prepend(CXXFLAGS=["-I. -include core/pch.h.gch"])
Export("scene_env")

# Chain load SCsubs
SConscript("main/SCsub")
SConscript("gui/SCsub")
SConscript("3d/SCsub")
SConscript("2d/SCsub")
SConscript("animation/SCsub")
SConscript("audio/SCsub")
SConscript("resources/SCsub")
SConscript("debugger/SCsub")


# Build it all as a library
lib = scene_env.add_library("scene", env.scene_sources)
env.Prepend(LIBS=[lib])
